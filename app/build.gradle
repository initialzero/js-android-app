/*
 * Copyright Â© 2014 TIBCO Software, Inc. All rights reserved.
 *  http://community.jaspersoft.com/project/jaspermobile-android
 *
 *  Unless you have purchased a commercial license agreement from Jaspersoft,
 *  the following license terms apply:
 *
 *  This program is part of Jaspersoft Mobile for Android.
 *
 *  Jaspersoft Mobile is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Jaspersoft Mobile is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with Jaspersoft Mobile for Android. If not, see
 *  <http://www.gnu.org/licenses/lgpl>.
 */

buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:" + androidToolsBuildGradle
        classpath "com.neenbedankt.gradle.plugins:android-apt:" + androidApt
        classpath 'org.robolectric:robolectric-gradle-plugin:1.0.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply from: 'build_utils.gradle'
apply from: 'resources.gradle'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId PACKAGE_NAME
        testApplicationId TEST_PACKAGE_NAME
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion

        testInstrumentationRunner INSTRUMENTATION_RUNNER
    }

    if (project.hasProperty('JMA_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(JMA_STORE_FILE)
                storePassword JMA_STORE_PASSWORD
                keyAlias JMA_KEY_ALIAS
                keyPassword JMA_KEY_PASSWORD
            }
        }

        buildTypes {
            release {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), "proguard.cfg"
                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    sourceSets.main {
        // src/gen is the target for generated content like json model
        java.srcDirs += 'build/generated/source/db'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        qa {
        }
        dev {
        }
        production {
            buildTypes {
                debug {
                    if (project.hasProperty('JMA_STORE_FILE')) {
                        signingConfig signingConfigs.release
                    }
                }
            }
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName PACKAGE_NAME
        guiceAnnotationDatabasePackageName "com.jaspersoft.android.jaspermobile"
        logLevel "WARN"
    }
}


dependencies {
    provided project(':database-generator')
    compile project(':sdk')
    compile project(':cordova')

    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'

    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'

    // For sugar instantiate of dialogs
    compile('eu.inmite.android.lib:android-styled-dialogs:1.2.0:') {
        exclude group: 'com.android.support'
    }

    compile("org.androidannotations:androidannotations-api:3.2")
    apt "org.androidannotations:androidannotations:3.2"
    apt 'org.roboguice:roboblender:3.0.1'

    // For action mode support within list views
    compile('com.github.manuelpeinado.multichoiceadapter:multichoiceadapter-abc:3.1.0') {
        exclude group: 'com.android.support'
    }

    // Splunk Mint(Bugsense)
    compile "com.splunk.mint:mint:4.0.3"

    // Remove gradle dependency
    // When all required testing for JRS will be completed
    compile 'com.google.code.gson:gson:2.3'

    // Dependency for image loading management
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'

    // Template rendering
    compile 'com.samskivert:jmustache:1.10'

    // Unit tests dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.retrofit:retrofit-mock:1.8.0'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile ('org.robolectric:robolectric:2.4') {
        exclude module: 'classworlds'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-plugin-registry'
        exclude module: 'maven-profile'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'nekohtml'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'support-v4' // crazy but my android studio don't like this dependency and to fix it remove .idea and re import project
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-http-shared'
        exclude module: 'wagon-provider-api'
    }

    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.1.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'

    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('org.mockito:mockito-core:1.9.5') {
        exclude group: "org.hamcrest"
    }
    androidTestCompile('com.google.dexmaker:dexmaker:1.0') {
        exclude group: "org.mockito"
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.0') {
        exclude group: "org.mockito"
    }
}
buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:" + androidToolsBuildGradle
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.1-SNAPSHOT'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.3'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'android-apt'
apply from: 'build_utils.gradle'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId PACKAGE_NAME
        testApplicationId TEST_PACKAGE_NAME
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 15020000
        versionName generateVersionName()
        testInstrumentationRunner INSTRUMENTATION_RUNNER
    }

    if (project.hasProperty('JMA_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(JMA_STORE_FILE)
                storePassword JMA_STORE_PASSWORD
                keyAlias JMA_KEY_ALIAS
                keyPassword JMA_KEY_PASSWORD
            }
        }

        buildTypes {
            release {
                runProguard true
                proguardFile "proguard.cfg"
                proguardFile getDefaultProguardFile('proguard-android.txt')
                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    applicationVariants.all { variant ->
        renameApk(variant)
        variant.assemble.doLast {
            removeUnaligned(variant)
        }
    }

    sourceSets.main {
        // src/gen is the target for generated content like json model
        java.srcDirs += 'build/generated/source/db'
    }
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName PACKAGE_NAME
    }
}


dependencies {
    compile 'com.jaspersoft.android.sdk:js-android-sdk-client:2.0-SNAPSHOT'
    compile 'com.jaspersoft.android.sdk:js-android-sdk-ui:2.0-SNAPSHOT'
    compile 'org.roboguice:roboguice:2.0'
    compile 'com.google.guava:guava:18.0'
    compile('com.android.support:support-v4:20.0.0') {
        exclude group: 'com.android.support'
    }
    // This is required for @Nullable @NonNull annotations
    compile 'com.android.support:support-annotations:20.0.0'
    // For sugar instantiate of dialogs
    compile('eu.inmite.android.lib:android-styled-dialogs:1.2.0:') {
        exclude group: 'com.android.support'
    }

    compile("org.androidannotations:androidannotations-api:${androidAnnotationsVersion}")
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"

    // Floating labels
    compile 'com.marvinlabs:android-floatinglabel-widgets:1.3.2@aar'

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.google.guava'
    }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.0'
    androidTestCompile('org.mockito:mockito-core:1.9.5') {
        exclude group: "org.hamcrest"
    }
    androidTestCompile('com.google.dexmaker:dexmaker:1.0') {
        exclude group: "org.mockito"
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.0') {
        exclude group: "org.mockito"
    }
}
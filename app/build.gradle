/*
 * Copyright Â© 2014 TIBCO Software, Inc. All rights reserved.
 *  http://community.jaspersoft.com/project/jaspermobile-android
 *
 *  Unless you have purchased a commercial license agreement from Jaspersoft,
 *  the following license terms apply:
 *
 *  This program is part of Jaspersoft Mobile for Android.
 *
 *  Jaspersoft Mobile is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Jaspersoft Mobile is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with Jaspersoft Mobile for Android. If not, see
 *  <http://www.gnu.org/licenses/lgpl>.
 */

buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:" + androidToolsBuildGradle
        classpath "com.neenbedankt.gradle.plugins:android-apt:" + androidApt
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply from: 'build_utils.gradle'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId PACKAGE_NAME
        testApplicationId TEST_PACKAGE_NAME
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
//        Pattern for versionCode:
//        android:versionCode="vvxxyyzz"
//        where:
//        vv - min android sdk version
//        xx - major version number
//        yy - minor version number
//        zz - revision number
        versionCode 15010900
        versionName generateVersionName()
        testInstrumentationRunner INSTRUMENTATION_RUNNER
    }

    if (project.hasProperty('JMA_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(JMA_STORE_FILE)
                storePassword JMA_STORE_PASSWORD
                keyAlias JMA_KEY_ALIAS
                keyPassword JMA_KEY_PASSWORD
            }
        }

        buildTypes {
            release {
                minifyEnabled true
                proguardFile "proguard.cfg"
                proguardFile getDefaultProguardFile('proguard-android.txt')
                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    sourceSets.main {
        // src/gen is the target for generated content like json model
        java.srcDirs += 'build/generated/source/db'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        qa {
        }
        dev {
        }
        production {
            versionCode 15010901
            versionName "1.9"

            buildTypes {
                debug {
                    if (project.hasProperty('JMA_STORE_FILE')) {
                        signingConfig signingConfigs.release
                    }
                }
            }
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName PACKAGE_NAME
    }
}


dependencies {
    provided project(':database-generator')
    compile project(':library-circular')

    compile 'com.jaspersoft.android.sdk:js-android-sdk-client:1.9.1-SNAPSHOT'
    compile 'com.jaspersoft.android.sdk:js-android-sdk-ui:1.9'
    compile 'org.roboguice:roboguice:2.0'
    compile 'com.google.guava:guava:18.0'
    compile('com.android.support:support-v4:20.0.0') {
        exclude group: 'com.android.support'
    }
    // This is required for @Nullable @NonNull annotations
    compile 'com.android.support:support-annotations:20.0.0'
    // For sugar instantiate of dialogs
    compile('eu.inmite.android.lib:android-styled-dialogs:1.2.0:') {
        exclude group: 'com.android.support'
    }

    compile("org.androidannotations:androidannotations-api:${androidAnnotationsVersion}")
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"

    // For action mode support within list views
    compile 'com.github.manuelpeinado.multichoiceadapter:multichoiceadapter:3.1.0'
    // For homogeneous design on all platforms
    compile 'com.negusoft.holoaccent:library:1.1'

    // Splunk Mint(Bugsense)
    compile "com.splunk.mint:mint:4.0.3"

    // Remove gradle dependency
    // When all required testing for JRS will be completed
    compile 'com.google.code.gson:gson:1.7.2'

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.google.guava'
    }
    androidTestCompile('org.mockito:mockito-core:1.9.5') {
        exclude group: "org.hamcrest"
    }
    androidTestCompile('com.google.dexmaker:dexmaker:1.0') {
        exclude group: "org.mockito"
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.0') {
        exclude group: "org.mockito"
    }
}